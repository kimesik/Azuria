//The antipope. The evil twin sibling of Bishop.
//Locked to Inhumen. Powerful support class with, however, limited combat potential.
//Gets the ability to torture, combined with EVIL sermons, ability to contact co-believers of same patron and some extra miracles from other Inhumen patrons.
#define EVIL_PRIEST_SERMON_COOLDOWN (30 MINUTES)
#define EVIL_PRIEST_ANNOUNCEMENT_COOLDOWN (5 MINUTES)
/datum/advclass/wretch/antipope
	name = "Heresiarch"
	tutorial = "They are pretentious. They are weak. They are complacent. And they are hopeless. But you. You will change this. \
	A high-ranking official of the Holy Ecclesial, for your deeds you have been blessed by the Four Ascendants to bring upon change and be their God Hand. \
	But this change will be resisted. Crush the dissent. Show them why it is better to rule in Gehenna than serve under the Firmament."
	allowed_sexes = list(MALE, FEMALE)
	outfit = /datum/outfit/job/roguetown/wretch/antipope
	subclass_languages = list(/datum/language/undead, /datum/language/thievescant)
	cmode_music = 'sound/music/combat_cult.ogg'
	class_select_category = CLASS_CAT_CLERIC
	category_tags = list(CTAG_WRETCH)
//Seer to see other Inhumen.
	traits_applied = list(TRAIT_HERETIC_SEER, TRAIT_RITUALIST, TRAIT_GRAVEROBBER, TRAIT_GODHAND)
//Support class statline, +12 weighted total on par with plate armour Heretics. No armour traits, DE or CR, so needs good stats desperately.
	subclass_stats = list(
		STATKEY_INT = 4,
		STATKEY_CON = 2,
		STATKEY_WIL = 2,
		STATKEY_SPD = 2,
	)
	maximum_possible_slots = 1//THERE CAN BE ONLY ONE GOD HAND.
	subclass_skills = list(
		/datum/skill/misc/reading = SKILL_LEVEL_LEGENDARY,
		/datum/skill/combat/wrestling = SKILL_LEVEL_EXPERT, //For self-defence, no STR so can't grab well, only resist
		/datum/skill/combat/unarmed = SKILL_LEVEL_EXPERT,
		/datum/skill/combat/polearms = SKILL_LEVEL_EXPERT,
		/datum/skill/combat/knives = SKILL_LEVEL_EXPERT,
		/datum/skill/misc/medicine = SKILL_LEVEL_EXPERT,
		/datum/skill/magic/holy = SKILL_LEVEL_MASTER, //You are Ascendants' chosen.
		/datum/skill/craft/crafting = SKILL_LEVEL_JOURNEYMAN,
		/datum/skill/craft/sewing = SKILL_LEVEL_JOURNEYMAN,
		/datum/skill/craft/cooking = SKILL_LEVEL_JOURNEYMAN,
		/datum/skill/misc/swimming = SKILL_LEVEL_JOURNEYMAN,
		/datum/skill/misc/climbing = SKILL_LEVEL_JOURNEYMAN,
		/datum/skill/misc/athletics = SKILL_LEVEL_JOURNEYMAN,
	)
	extra_context = "Inhumen exclusive. Given EVIL sermon abilities, torture, broadcasting to co-believers, maxed out miracles of their own patron and some extra miracles from other Inhumen patrons."
	subclass_stashed_items = list(
		"Lexicon of Her Truth" = /obj/item/book/rogue/bibble/zizo,
		"Sewing Kit" =  /obj/item/repair_kit,
	)
/datum/outfit/job/roguetown/wretch/antipope
	has_loadout = TRUE

//Starts with some basic leather armour.
/datum/outfit/job/roguetown/wretch/antipope/pre_equip(mob/living/carbon/human/H)
	if(!istype(H.patron, /datum/patron/inhumen))
		H.set_patron(/datum/patron/inhumen/zizo)//If you're not of the Inhumen before? You are now!
	neck = /obj/item/clothing/neck/roguetown/leather
	shoes = /obj/item/clothing/shoes/roguetown/boots/leather/reinforced
	pants = /obj/item/clothing/under/roguetown/heavy_leather_pants
	wrists = /obj/item/clothing/wrists/roguetown/bracers/cloth/monk
	shirt = /obj/item/clothing/suit/roguetown/armor/gambeson
	armor = /obj/item/clothing/suit/roguetown/shirt/robe/monk
	belt = /obj/item/storage/belt/rogue/leather
	beltl = /obj/item/storage/belt/rogue/pouch/coins/rich //Hire Atgervis!
	backl = /obj/item/storage/backpack/rogue/backpack
	backr = /obj/item/rogueweapon/woodstaff/quarterstaff
	if(istype(H.patron, /datum/patron/inhumen/zizo))
		id = /obj/item/clothing/neck/roguetown/psicross/inhumen/g
	if(istype(H.patron, /datum/patron/inhumen/graggar))
		id = /obj/item/clothing/neck/roguetown/psicross/inhumen/graggar
	if(istype(H.patron, /datum/patron/inhumen/baotha))
		id = /obj/item/clothing/neck/roguetown/psicross/inhumen/baotha
	if(istype(H.patron, /datum/patron/inhumen/matthios))
		id = /obj/item/clothing/neck/roguetown/psicross/inhumen/matthios
	backpack_contents = list(
		/obj/item/flashlight/flare/torch/lantern/prelit = 1,
		/obj/item/rope/chain = 1,
		/obj/item/ritechalk = 1,
		/obj/item/reagent_containers/glass/bottle/alchemical/healthpot = 1,	//Small health vial
		/obj/item/rogueweapon/scabbard/sheath = 1,
		/obj/item/clothing/head/roguetown/helmet/heavy/heresiarch = 1,
		/obj/item/rogueweapon/huntingknife/idagger/steel/zizo = 1, //I have to put these two in the backpack, else you instantly get set on flames.
	)
	if(H.age == AGE_OLD)
		H.adjust_skillrank_up_to(/datum/skill/magic/holy, 6, TRUE)

	if(H.mind)
		wretch_select_bounty(H)
		H.mind.AddSpell(new /datum/action/cooldown/spell/convert_heretic)
		H.mind.AddSpell(new /datum/action/cooldown/spell/miracle/intervention)
		H.mind.AddSpell(new /obj/effect/proc_holder/spell/invoked/silence)//Shut that guy up!
		H.mind.AddSpell(new /datum/action/cooldown/spell/nondetection)//To meet up with folks.
		H.mind.AddSpell(new /datum/action/cooldown/spell/message)//See above.
		H.mind.AddSpell(new /obj/effect/proc_holder/spell/invoked/baothavice)//Trivial but great roleplaying potential.
		H.verbs |= /mob/living/carbon/human/proc/completesermon_evil
		H.verbs |= /mob/living/carbon/human/proc/revelations
		H.verbs |= /mob/living/carbon/human/proc/heresiarch_broadcast

	var/datum/devotion/C = new /datum/devotion(H, H.patron)
	C.grant_miracles(H, cleric_tier = CLERIC_T4, passive_gain = CLERIC_REGEN_MAJOR, start_maxed = TRUE)	//Starts off maxed out.
	H.dna.species.soundpack_m = new /datum/voicepack/male/wizard()

/datum/outfit/job/roguetown/wretch/antipope/choose_loadout(mob/living/carbon/human/H)
	. = ..()
	var/t3_count = 1
	var/t2_count = 1
	var/t1_count = 1
	var/t0_count = 1
	var/list/t3 = list()
	var/list/t2 = list()
	var/list/t1 = list()
	var/list/t0 = list()
	for(var/path as anything in GLOB.patrons_by_faith[/datum/faith/inhumen])
		var/datum/patron/patron = GLOB.patronlist[path]
		if(!patron || !patron.name)
			continue
		for(var/miracle in patron.miracles)
			var/obj/effect/proc_holder/checked_miracle = miracle
			if(patron.miracles[checked_miracle] == CLERIC_T3)
				t3[initial(checked_miracle.name)] = checked_miracle
			if(patron.miracles[checked_miracle] == CLERIC_T2)
				t2[initial(checked_miracle.name)] = checked_miracle
			if(patron.miracles[checked_miracle] == CLERIC_T1)
				t1[initial(checked_miracle.name)] = checked_miracle
			if(patron.miracles[checked_miracle] == CLERIC_T0)
				t0[initial(checked_miracle.name)] = checked_miracle
	for(var/miracle in t3)
		if(H.mind?.has_spell(t3[miracle]))
			t3.Remove(miracle)
	for(var/miracle in t2)
		if(H.mind?.has_spell(t2[miracle]))
			t2.Remove(miracle)
	for(var/miracle in t1)
		if(H.mind?.has_spell(t1[miracle]))
			t1.Remove(miracle)
	for(var/miracle in t0)
		if(H.mind?.has_spell(t0[miracle]))
			t0.Remove(miracle)
	for(var/i in 1 to t3_count)
		var/t3_choice = input(H,"Choose your Tier Three Miracle.", "TAKE UP DARK KNOWLEDGE ([t3_count] CHOICES REMAIN)") as anything in t3
		if(t3_choice)
			var/obj/effect/proc_holder/chosen_miracle = t3[t3_choice]
			H.mind?.AddSpell(new chosen_miracle)
			t3.Remove(t3_choice)
			t3_count--
	for(var/i in 1 to t2_count)
		var/t2_choice = input(H,"Choose your Tier Two Miracle.", "TAKE UP DARK KNOWLEDGE ([t2_count] CHOICES REMAIN)") as anything in t2
		if(t2_choice)
			var/obj/effect/proc_holder/chosen_miracle = t2[t2_choice]
			H.mind?.AddSpell(new chosen_miracle)
			t2.Remove(t2_choice)
			t2_count--
	for(var/i in 1 to t1_count)
		var/t1_choice = input(H,"Choose your Tier One Miracle.", "TAKE UP DARK KNOWLEDGE ([t1_count] CHOICES REMAIN)") as anything in t1
		if(t1_choice)
			var/obj/effect/proc_holder/chosen_miracle = t1[t1_choice]
			H.mind?.AddSpell(new chosen_miracle)
			t1.Remove(t1_choice)
			t1_count--
	for(var/i in 1 to t0_count)
		var/t0_choice = input(H,"Choose your Tier Zero Miracle.", "TAKE UP DARK KNOWLEDGE ([t0_count] CHOICES REMAIN)") as anything in t0
		if(t0_choice)
			var/obj/effect/proc_holder/chosen_miracle = t0[t0_choice]
			H.mind?.AddSpell(new chosen_miracle)
			t0.Remove(t0_choice)
			t0_count--

	if(H.mind?.has_spell(/datum/action/cooldown/spell/raise_undead_formation/zizo) || H.mind?.has_spell(/datum/action/cooldown/spell/tame_undead/zizo))
		H.mind.AddSpell(new /datum/action/cooldown/spell/minion_order)
		H.mind.AddSpell(new /datum/action/cooldown/spell/gravemark)
		H.mind?.current.faction += "[H.name]_faction"

/mob/living/carbon/human/proc/completesermon_evil()
	set name = "Inhumen Sermon"
	set category = "Antipope"

	if (!mind)
		return

	//ANYWHERE, really, EXCEPT the chapel.
	if (istype(get_area(src), /area/rogue/indoors/town/church/chapel))
		to_chat(src, span_warning("I can't do this here! They'll know!"))
		return FALSE

	if (!COOLDOWN_FINISHED(src, evil_priest_sermon))
		to_chat(src, span_warning("You cannot inspire others so early."))
		return

	src.visible_message(span_notice("[src] begins preaching a sermon..."))

	if (!do_after(src, 120, target = src)) // 120 seconds
		src.visible_message(span_warning("[src] stops preaching."))
		return

	src.visible_message(span_notice("[src] finishes the sermon, inspiring those nearby!"))
	playsound(src.loc, 'sound/magic/ahh2.ogg', 80, TRUE)
	COOLDOWN_START(src, evil_priest_sermon, EVIL_PRIEST_SERMON_COOLDOWN)

	for (var/mob/living/carbon/human/H in view(7, src))
		if (!H.patron)
			continue
		//We invert the sermon positives and negatives. Wild how that works.
		if (istype(H.patron, /datum/patron/divine) && !HAS_TRAIT(H, TRAIT_HERESIARCH)) //Tennite Wretches won't be affected for the sake of convenience.
			H.apply_status_effect(/datum/status_effect/debuff/hereticsermon)
			H.add_stress(/datum/stressevent/heretic_on_sermon)
			to_chat(H, span_warning("Your patron seethes with disapproval."))
		else if (istype(H.patron, /datum/patron/inhumen))
			H.apply_status_effect(/datum/status_effect/buff/sermon)
			H.add_stress(/datum/stressevent/sermon)
			to_chat(H, span_notice("You feel a divine affirmation from your patron."))
		else
			// Other patrons - fluff only
			to_chat(H, span_notice("Nothing seems to happen to you."))

	return TRUE

/mob/living/carbon/human/proc/heresiarch_broadcast()
	set name = "Dark Whisper"
	set category = "Antipope"

	if(stat)
		return

//	var/found_structure = FALSE
//	var/list/search_area = oview(5, src)
//
//	for(var/atom/A in search_area)
//		// Check if the atom itself is the required structure type
//		if(istype(A, /obj/structure/fluff/psycross/zizocross))
//			found_structure = TRUE
//			break
//		if(istype(A, /turf))
//			var/turf/T = A
//			for(var/obj/O in T.contents)
//				if(istype(O, /obj/structure/fluff/psycross/zizocross))
//					found_structure = TRUE
//					break // Found it in the turf, no need to check further
//			if(found_structure)
//				break
//
//	if(!found_structure)
//		to_chat(src, span_warning("I need a large profane shrine structure nearby to reach their minds!"))
//		return

	var/announcementinput = input("Let your voice reach all those who bow before the same god you do", "Speak, they will hear.") as text|null
	if(announcementinput)
		if(!src.can_speak_vocal())
			to_chat(src, span_warning("I can't speak!"))
			return FALSE
		if(!COOLDOWN_FINISHED(src, evil_priest_announcement))
			to_chat(src, span_warning("You must wait before speaking again."))
			return FALSE
		visible_message(span_warning("[src] silently mouths words, preparing to whisper into enlightened minds..."))
		if(do_after(src, 15 SECONDS, target = src))
			say(announcementinput)
			for(var/mob/living/carbon/human/M in GLOB.human_list)
				if(M.patron?.type == src.patron?.type)
					to_chat(M, span_boldnotice("<big><b>Your dark shepherd whispers:</b> [announcementinput]</big>"))
					M.playsound_local(M, 'sound/magic/zizo_snuff.ogg', 100)
			COOLDOWN_START(src, evil_priest_announcement, EVIL_PRIEST_ANNOUNCEMENT_COOLDOWN)
		else
			to_chat(src, span_warning("Your whispering was interrupted!"))
			return FALSE
